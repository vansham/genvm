{ pkgs
, zig
, ...
}@args0:
let
	lib = pkgs.lib;
	importCargoLock = pkgs.rustPlatform.importCargoLock;
	fetchCargoTarball = pkgs.rustPlatform.fetchCargoTarball;
	fetchCargoVendor = pkgs.rustPlatform.fetchCargoVendor;
	stdenv = pkgs.stdenv;
	callPackage = pkgs.callPackage;
	cargoBuildHook = pkgs.rustPlatform.cargoBuildHook;
	cargoInstallHook = pkgs.rustPlatform.cargoInstallHook;
	cargoSetupHook = pkgs.rustPlatform.cargoSetupHook;
	cargo = pkgs.cargo;
	cargo-auditable = pkgs.cargo-auditable;
	buildPackages = pkgs.buildPackages;
	libiconv = pkgs.libiconv;
	windows = pkgs.windows;
in {
	name ? "${args.pname}-${args.version}",

	# Name for the vendored dependencies tarball
	cargoDepsName ? name,

	src ? null,
	srcs ? null,
	preUnpack ? null,
	unpackPhase ? null,
	postUnpack ? null,
	cargoPatches ? [ ],
	patches ? [ ],
	sourceRoot ? null,
	logLevel ? "",
	buildInputs ? [ ],
	nativeBuildInputs ? [ ],
	cargoUpdateHook ? "",
	cargoDepsHook ? "",
	profile ? "release",
	meta ? { },
	cargoLock,
	buildNoDefaultFeatures ? false,
	buildFeatures ? [ ],
	auditable ? !cargo-auditable.meta.broken,

	extraLibs ? [ ],

	depsExtraArgs ? { },

	# Toggles whether a custom sysroot is created when the target is a .json file.
	__internal_dontAddSysroot ? false,

	# Needed to `pushd`/`popd` into a subdir of a tarball if this subdir
	# contains a Cargo.toml, but isn't part of a workspace (which is e.g. the
	# case for `rustfmt`/etc from the `rust-sources).
	# Otherwise, everything from the tarball would've been built/tested.
	buildAndTestSubdir ? null,

	target,
	installPhase ? null,
	...
}@args:
let
	targetAsRust = {
		amd64-linux = "x86_64-unknown-linux-musl";
		arm64-linux = "aarch64-unknown-linux-musl";
		arm64-macos = "aarch64-apple-darwin";

		x86_64-linux = "x86_64-unknown-linux-gnu";
	}.${target};

	rust-pkg = import ./rust.nix args0;
in

stdenv.mkDerivation (
	(removeAttrs args [
		"depsExtraArgs"
		"cargoUpdateHook"
		"cargoDeps"
		"cargoLock"
	])
	// {
		cargoDeps = importCargoLock cargoLock;
		inherit buildAndTestSubdir;

		RUSTFLAGS =
			"-C target-feature=-crt-static -l dylib=c -L /build/libs -C link-arg=-dynamic "
			+ (args.RUSTFLAGS or "");

		hardeningDisable = ["all"];

		cargoBuildNoDefaultFeatures = buildNoDefaultFeatures;

		cargoBuildFeatures = buildFeatures;

		nativeBuildInputs =
			nativeBuildInputs
			++ [
				cargoSetupHook
				rust-pkg
				zig
				pkgs.glibc
				pkgs.libllvm
			];

		buildInputs =
			buildInputs
			++ lib.optionals stdenv.hostPlatform.isDarwin [ libiconv ];

		patches = cargoPatches ++ patches;

		PKG_CONFIG_ALLOW_CROSS = 1;

		postUnpack =
			''
				eval "$cargoDepsHook"

				mkdir -p /build/libs

				export RUST_LOG=${logLevel}
			''
			+ (args.postUnpack or "")
			+ "\n"
			+ builtins.concatStringsSep "\n" (
				builtins.map (x: "cp ${x}/lib/* /build/libs/") extraLibs
			);

		configurePhase =
			args.configurePhase or ''
				runHook preConfigure
				runHook postConfigure
			'';

		doCheck = false;

		strictDeps = true;

		meta = meta;

		buildPhase = ''
			runHook preBuild

			ls -l /build/libs/

			echo "PATH=$PATH"
			echo "RUSTFLAGS=$RUSTFLAGS"
			echo cargo build --target ${targetAsRust} -j $NIX_BUILD_CORES --offline --profile=${profile}
			cargo build --target ${targetAsRust} -j $NIX_BUILD_CORES --offline --profile=${profile}
			runHook postBuild

			bins=$(find target/${targetAsRust}/${profile}/ \
					-maxdepth 1 \
					-type f \
					-executable -not -regex ".*\.\(so.[0-9.]+\|so\|a\|dylib\)" )
			echo "Found binary $bins"

			cp "$bins" target/__out

			if [[ "${target}" != arm64-macos ]]
			then
				patchelf --set-rpath '$ORIGIN/../lib:$ORIGIN/../../../lib:' target/__out

				for i in $(patchelf --print-needed target/__out)
				do
					if [[ "$i" == /build/libs/* ]]
					then
						echo "Replacing $i with $(basename $i)"
						patchelf --replace-needed "$i" "$(basename $i)" target/__out
					fi
				done
			fi
		'';

		installPhase = if installPhase != null then installPhase else ''
			cp "target/__out" "$out"
		'';

		dontFixup = true;
	}
)
